Shader "shader lab/week 10/pixelate" {
    Properties {
       _resolution("resolution", Int) = 128
    }
    SubShader {
        Tags { "RenderPipeline"="UniversalPipeline" }
        
        ZWrite Off
        Cull Off
        ZTest Always
        
        Pass {
            HLSLPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"

            CBUFFER_START(UnityPerMaterial)
            int _resolution;            
            CBUFFER_END

            TEXTURE2D(_BlitTexture);
            SAMPLER(sampler_BlitTexture);
            
            struct MeshData {
                uint vertexID : SV_VertexID;
            };
            
            struct Interpolators {
                float4 posCS : SV_POSITION;
                float2 uv : TEXCOORD0;
            };

            Interpolators vert (MeshData v) {
                Interpolators o;
                o.posCS = GetFullScreenTriangleVertexPosition(v.vertexID);
                o.uv    = GetFullScreenTriangleTexCoord   (v.vertexID);
                return o;
            }
            
            float4 frag (Interpolators i) : SV_Target {
                float2 uv = i.uv;
                float3 color = 0;

                float aspect = _ScreenParams.x / _ScreenParams.y;
                uv.x *= aspect;
                
                uv = floor(uv * _resolution) / _resolution;

                uv.x /= aspect;
                
                color = SAMPLE_TEXTURE2D(_BlitTexture, sampler_BlitTexture, uv);
                
                return float4(color, 1.0);
            }
            ENDHLSL
        }
    }
}